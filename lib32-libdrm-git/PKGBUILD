# Arch libdrm Maintainers:
#Jan de Groot
#Andreas Radke

_realver=2.4.46
_gitroot='git://git.freedesktop.org/git/mesa/drm'
_gitname='drm'

pkgname=lib32-libdrm-git
pkgver=20130705
pkgrel=1137
pkgdesc="Userspace interface to kernel DRM services"
arch=('i686' 'x86_64')
license=('MIT')
groups=('lib32')
depends=('lib32-libpciaccess')
makedepends=('gcc-multilib' 'git' 'cairo' 'udev')
options=('!libtool')
url="http://dri.freedesktop.org/"
provides=('lib32-libdrm=${_realver}')
conflicts=('lib32-libdrm')
source=($_gitroot)
md5sums=(SKIP)

build() {
  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  sed -i 's/PKG_CHECK_MODULES(PTHREADSTUBS, pthread-stubs)//' configure.ac

  ./autogen.sh \
    --build=x86_64-pc-linux-gnu \
    --host=i686-pc-linux-gnu \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --enable-udev \
    --enable-intel \
    --enable-radeon \
    --enable-nouveau \
    --enable-vmwgfx

  make
}

package() {
  cd "$srcdir/$_gitname"
  make DESTDIR="$pkgdir" install
  rm -rf "${pkgdir}/usr/include" "${pkgdir}/usr/share"
}
# vim: ft=sh syn=sh et

