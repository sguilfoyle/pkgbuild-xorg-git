# Maintainer: twa022 <twa022 at gmail dot com>
# based on nautilus-dropbox package, contributors below...
# Contributor: josephgbr <rafael.f.f1@gmail.com>
# Contributor: cmorlok <christianmorlok@web.de>
# Contributor: fazibear <fazibear@gmail.com>
# Contributor: neuromante <lorenzo.nizzi.grifi@gmail.com>
# Contributor: Gordin <9ordin @t gmail.com>

_pkgname=nemo-dropbox
pkgname=nemo-dropbox-git
pkgver=20121207
pkgrel=$(date +"%H%M")
pkgdesc="Dropbox for Linux - Nemo extension"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo-extensions"
license=('custom:CC-BY-ND-3' 'GPL')
depends=('libnotify' 'nemo-git' 'dropbox' 'hicolor-icon-theme')
makedepends=('python2-docutils' 'python2' 'pygtk')
install=${_pkgname}.install
options=('!libtool' '!emptydirs')

_gitroot=git://github.com/linuxmint/nemo-extensions.git
_gitname=nemo-extensions

build() {
  cd "${srcdir}"
  msg 'Connecting to GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git checkout -f && git clean -xfd && git pull origin
  else
    git clone --depth 1 ${_gitroot} ${_gitname}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd "${srcdir}/${_gitname}"



  cd "${_pkgname}"

  autoreconf -fi

  sed -i "s:python:&2:" \
    configure \
    Makefile.am \
    dropbox.in \
    rst2man.py \
    Makefile.in

  # since python2-docutils, rst2man.py is named "rst2man2.py"
  sed -i "s:rst2man.py:/usr/bin/rst2man2.py:" configure

  PYTHON=python2 ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd "${srcdir}/${_gitname}/${_pkgname}"
  make DESTDIR="${pkgdir}" install
  rm "${pkgdir}/usr/bin/dropbox"
  rm "${pkgdir}/usr/share/applications/dropbox.desktop"
  rm "${pkgdir}/usr/share/man/man1/dropbox.1.gz"
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${_pkgname}/COPYING"
}
