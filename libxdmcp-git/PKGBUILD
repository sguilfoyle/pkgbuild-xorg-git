# $Id: PKGBUILD 152771 2012-03-09 16:07:19Z andyrtr $
#Maintainer: Jan de Groot <jgc@archlinux.org>

pkgname=libxdmcp-git
pkgver=20130106
pkgrel=0
pkgdesc="X11 Display Manager Control Protocol library"
arch=('i686' 'x86_64')
url="http://xorg.freedesktop.org/"
license=('custom')
depends=('xproto-git' 'glibc')
makedepends=('xorg-util-macros-git')
provides=('libxdmcp=1.1.1')
conflicts=('libxdmcp')
options=('!libtool')

_gitroot="git://anongit.freedesktop.org/xorg/lib/libXdmcp"
_gitname="libxdmcp"

build() {
  cd "${srcdir}"
  msg 'Connecting to GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git checkout -f && git pull origin
  else
    git clone --depth 1 ${_gitroot} ${_gitname}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd "${srcdir}/${_gitname}"


  ./autogen.sh --prefix=/usr --sysconfdir=/etc --disable-static
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
