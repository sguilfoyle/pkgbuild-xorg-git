# Maintainer: Thomas Dziedzic < gostrc at gmail >
# Contributor: Roberto Alsina <ralsina@kde.org>
# Contributor: Tomas Lindquist Olsen <tomas@famolsen.dk>
# Contributor: Anders Bergh <anders@archlinuxppc.org>
# Contributor: Tomas Wilhelmsson <tomas.wilhelmsson@gmail.com>

pkgname=llvm-git
pkgver=20130308
pkgrel=1722
pkgdesc='Low Level Virtual Machine with AMDGPU enabled to build r600g shader llvm support'
arch=('i686' 'x86_64')
url="http://llvm.org"
license=('custom:University of Illinois/NCSA Open Source License')
depends=('libffi' 'python2' 'ocaml')
makedepends=('git')
provides=('llvm')
conflicts=('llvm')

# git so it can be useful
options=('!strip')

_gitroot="http://llvm.org/git/llvm"
_gitname="llvm"

build() {
  cd "${srcdir}"
  msg 'Connecting to GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git clean -xfd && git checkout -f && git pull origin
  else
    git clone --depth 1 ${_gitroot} ${_gitname}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd "${srcdir}/${_gitname}"


  sed -i 's/python$/python2/' utils/llvm-build/llvm-build

  # Include location of libffi headers in CPPFLAGS
  export CPPFLAGS="$CPPFLAGS $(pkg-config --cflags libffi)"

  # Force the use of GCC instead of clang
  PYTHON=python2 CC=gcc CXX=g++ \
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib/llvm \
    --sysconfdir=/etc \
    --enable-shared \
    --enable-libffi \
    --enable-targets=all \
    --enable-optimized \
    --enable-experimental-targets=R600 \
    --disable-expensive-checks \
    --with-binutils-include=/usr/include

  make REQUIRES_RTTI=1 ENABLE_OPTIMIZED=1
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="$pkgdir" install

  # Remove useless files
  rm -r "$pkgdir"/usr/{docs,lib/ocaml}
  # Fix permissions of static libs
  chmod -x "$pkgdir"/usr/lib/lib*.a

  install -Dm644 LICENSE.TXT "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
# vim: ft=sh syn=sh et

