# $Id: PKGBUILD 160633 2012-06-02 21:43:25Z andyrtr $
# Maintainer: Jan de Groot <jgc@archlinux.org>

_realver=1.6.2
_gitroot="git://anongit.freedesktop.org/xorg/lib/libX11"
_gitname="libX11"

pkgname=libx11-git
pkgver=20140129
pkgrel=1420
pkgdesc="X11 client-side library"
arch=(i686 x86_64)
url="http://xorg.freedesktop.org/"
depends=('libxcb>=1.9' 'xproto>=7.0.23' 'kbproto>=1.0.6')
makedepends=('xorg-util-macros-git' 'xextproto>=7.2.1' 'xtrans>=1.2.7' 'inputproto>=2.3')
provides=('libx11=${_realver}')
conflicts=('libx11')
options=('!libtool')
license=('custom')
source=($_gitroot
        xorg.sh
        xorg.csh)

sha256sums=(SKIP
            '1a1b5db0320579958fba24774bae7984a1b1b8c647ac6fbd4d8937178c48ed46'
            '96c85ccbdcb056540f2e2b6cf48dea067f80260c16957ee401c49cf94cea5894')

build() {
  cd "${srcdir}/${_gitname}"

  ./autogen.sh --prefix=/usr --disable-static --disable-xf86bigfont
  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/etc/profile.d"
  install -m755 "${srcdir}/"xorg.{sh,csh} "${pkgdir}/etc/profile.d/"

  install -d -m755 "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/"
}
# vim: ft=sh syn=sh et

