# Maintainer: Stefano Facchini <stefano.facchini@gmail.com>
# Contributor: Jonathan Lestrelin <zanko@daemontux.org>
# Derived from the spice-gtk PKGBUILD by
# Contributor: Lucio Zara <pennega@gmail.com>

pkgname=spice-gtk
_pkgname=spice-gtk
pkgver=0.14
pkgrel=1
pkgdesc="A Gtk client and libraries for SPICE remote desktop servers (gtk2 version)."
arch=('x86_64' 'i686')
url="http://spice-space.org"
license=('GPL')
makedepends=('intltool' 'vala' 'python2-pyparsing')
depends=('libpulse' 'gtk2' 'spice' 'libusb' 'usbredir')
conflicts=('spice-gtk3')
options=('!libtool')
source=("http://www.spice-space.org/download/gtk/$_pkgname-$pkgver.tar.bz2")
sha256sums=('a16fa27364541493b886cc943f72c8f96ec251083af0a29d405a2c4f490274f5')

build() {
  cd "$srcdir/$_pkgname-$pkgver"

  sed -i 's,/usr/bin/env python,/usr/bin/python2,' spice-common/spice_codegen.py

  PYTHON=python2 ./configure --prefix=/usr \
              --with-gtk=2.0 \
              --enable-vala \
              --with-python

  PYTHON=python2 make -j1
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make -j1 DESTDIR="$pkgdir/" install
}
