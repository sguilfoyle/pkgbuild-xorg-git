# Maintainer: Ner0

pkgname=nemo-git
pkgver=20121016
pkgrel=1
pkgdesc="Cinnamon file manager (Nautilus fork)"
arch=('i686' 'x86_64')
url="https://github.com/linuxmint/nemo"
license=('GPL')
depends=('dconf' 'desktop-file-utils' 'exempi' 'gnome-desktop' 'gnome-icon-theme' 'hicolor-icon-theme' 'libnotify' 'libtracker-sparql')
makedepends=('git' 'gtk-doc' 'gobject-introspection' 'intltool')
optdepends=('nemo-dropbox-git: Dropbox integration'
             'nemo-fileroller-git: File Roller integration')
conflicts=('nemo-fm')
provides=('nemo-fm')
options=('!emptydirs' '!libtool')
install=nemo.install

_gitroot=git://github.com/linuxmint/nemo.git
_gitname=nemo

build() {
  msg "Connecting to GIT server...."

  if [[ -d "$_gitname" ]]; then
    cd "$_gitname" && git pull origin && cd ..
    msg "The local files are updated."
  else
    git clone "$_gitroot" "$_gitname"
  fi

  msg "GIT checkout done or server timeout"
  msg "Starting build..."

  rm -rf "$_gitname-build"
  git clone "$_gitname" "$_gitname-build"
  cd "$_gitname-build"

  ./autogen.sh --prefix=/usr --sysconfdir=/etc \
      --localstatedir=/var --disable-static \
      --libexecdir=/usr/lib/nemo \
      --disable-nst-extension \
      --disable-update-mimedb \
      --disable-packagekit \
      --disable-schemas-compile
  make
}

package() {
  cd "$_gitname-build"
  make DESTDIR="$pkgdir/" install
}

# vim:set ts=2 sw=2 et:
