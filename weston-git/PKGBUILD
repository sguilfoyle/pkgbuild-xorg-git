# Maintainer: Joel Teichroeb <joel@teichroeb.net>
# Contributor: Emmanuel Gil Peyrot <linkmauve@linkmauve.fr>

_realver=1.0.90
pkgname=weston-git
pkgver=20130308
pkgrel=1742
pkgdesc="Reference implementation of a Wayland compositor"
arch=(i686 x86_64)
url="http://wayland.freedesktop.org"
license=('MIT')
depends=('libxkbcommon' 'wayland' 'mesa' 'cairo'
         'poppler-glib' 'mtdev' 'libxcursor' 'pixman' 'glu')
options=(!libtool)
provides=('weston=${_realver}')

_gitroot="git://anongit.freedesktop.org/wayland/weston"
_gitname="weston"

build() {
  cd "${srcdir}"
  msg 'Connecting to GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git clean -xfd && git checkout -f && git pull origin
  else
    git clone --depth 1 ${_gitroot} ${_gitname}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd "${srcdir}/${_gitname}"


  ./autogen.sh \
    --prefix=/usr \
    --libexecdir=/usr/lib/weston \
    --disable-android-compositor

  make
}

package() {
  cd "${srcdir}/${_gitname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 COPYING "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"

  for _c in clickdot cliptest dnd editor eventdemo flower gears image resizor simple-egl simple-shm simple-touch smoke view; do
    install -Dm755 "clients/$_c" "${pkgdir}/usr/bin/weston-$_c"
  done
}

# vim: ft=sh syn=sh et
