# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgbase=lib32-mesa-git
pkgname=('lib32-libglapi-git' 'lib32-libgl-git' 'lib32-mesa-git' 'lib32-osmesa-git' 'lib32-libgbm-git' 'lib32-libgles-git' 'lib32-libegl-git' 'lib32-ati-dri-git' 'lib32-intel-dri-git' 'lib32-nouveau-dri-git')
pkgver=20130110
pkgrel=1154
arch=('x86_64')
makedepends=('glproto-git' 'lib32-libdrm-git' 'lib32-libxxf86vm>=1.1.2' 'lib32-libxdamage>=1.1.3' 'lib32-expat>=2.1.0' 'lib32-libx11>=1.5.0' 'lib32-libxt>=1.1.3'
             'gcc-multilib' 'dri2proto-git' 'python2' 'libxml2' 'imake' 'lib32-llvm-amdgpu-git' 'lib32-wayland-git' 'lib32-systemd')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=(LICENSE)
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a')

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa-build'
_mesaver=9.1

build() {
  cd "${srcdir}"
  msg 'Connecting to GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git checkout -f && git pull origin
  else
    git clone --depth 1 ${_gitroot} ${_gitname}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  # for our llvm-config for 32 bit
  export LLVM_CONFIG=/usr/bin/llvm-config32

  COMMONOPTS="--prefix=/usr \
  --sysconfdir=/etc \
  --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
  --with-gallium-drivers=r300,r600,radeonsi,nouveau,swrast \
  --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
  --enable-32-bit \
  --libdir=/usr/lib32 \
  --enable-gallium-llvm \
  --enable-egl \
  --enable-gallium-egl \
  --with-egl-platforms=drm,x11,wayland \
  --enable-shared-glapi \
  --enable-gbm \
  --enable-glx-tls \
  --enable-dri \
  --enable-glx \
  --enable-osmesa \
  --enable-gles1 \
  --enable-gles2 \
  --enable-texture-float \
  --enable-r600-llvm-compiler \
  "

  ./autogen.sh $COMMONOPTS

  make
}

package_lib32-libglapi-git() {
  depends=('glibc')
  conflicts=('lib32-libglapi')
  provides=("lib32-libglapi=${_mesaver}")
  pkgdesc="free implementation of the GL API -- shared library. The Mesa GL API module is responsible for dispatching all the gl* functions"

  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-libglapi"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-libglapi/"
}

package_lib32-libgl-git() {
  depends=('lib32-libdrm-git' 'lib32-libxxf86vm>=1.1.2' 'lib32-libxdamage>=1.1.3' 'lib32-expat>=2.1.0' 'lib32-libglapi-git' 'gcc-libs')
  pkgdesc="Mesa 3-D graphics library and DRI software rasterizer"
  conflicts=('lib32-libgl')
  provides=("lib32-libgl=${_mesaver}")

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  # libGL & libdricore
  make -C ${srcdir}/?esa-*/src/glx DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall


  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-swrast DESTDIR="${pkgdir}" install

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib32/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib32/xorg/modules/extensions/libglx.so"

  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-libgl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-libgl/"
}

package_lib32-mesa-git() {
  # check also gl.pc
  depends=('lib32-libgl-git' 'lib32-libx11>=1.5.0' 'lib32-libxext>=1.3.1' 'lib32-libxdamage' 'lib32-libxfixes' 'lib32-libxcb' 'lib32-libxxf86vm')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  pkgdesc="Mesa 3-D graphics libraries and include files"
  conflicts=('lib32-mesa')
  provides=("lib32-mesa=${_mesaver}")

  make -C ${srcdir}/?esa-*/src/mesa DESTDIR="${pkgdir}" install-glHEADERS
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri DESTDIR="${pkgdir}" install-driincludeHEADERS
  make -C ${srcdir}/?esa-*/src/mesa DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri DESTDIR="${pkgdir}" install-pkgconfigDATA
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/common DESTDIR="${pkgdir}" install-sysconfDATA

  rm -r "$pkgdir"/etc "$pkgdir"/usr/include
  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-mesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-mesa/"
}

package_lib32-osmesa-git() {
  depends=('lib32-libglapi-git' 'gcc-libs')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  pkgdesc="Mesa 3D off-screen rendering library"
  conflicts=('lib32-osmesa')
  provides=("lib32-osmesa=${_mesaver}")

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  make -C ${srcdir}/?esa-*/src/mesa/drivers/osmesa DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-osmesa"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-osmesa/"
}

package_lib32-libgbm-git() {
  depends=('systemd' 'lib32-libglapi-git' 'lib32-libdrm-git')
  pkgdesc="Mesa gbm library"
  conflicts=('lib32-libgbm')
  provides=("lib32-libgbm=${_mesaver}")

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  make -C ${srcdir}/?esa-*/src/gbm DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall

  rm -r "$pkgdir"/usr/include
  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-libgbm"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-libgbm/"
}

package_lib32-libgles-git() {
  depends=('lib32-libglapi-git' 'lib32-libdrm-git' 'khrplatform-devel-git')
  pkgdesc="Mesa GLES libraries and headers"
  conflicts=('lib32-libgles')
  provides=("lib32-libgles=${_mesaver}")

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install

  make -C ${srcdir}/?esa-*/src/mapi/es1api DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/mapi/es2api DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall

  rm -r "$pkgdir"/usr/include
  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-libgles"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-libgles/"
}

package_lib32-libegl-git() {
  # check also egl.pc
  depends=('lib32-libx11' 'lib32-libxext' 'lib32-libxdamage' 'lib32-libxfixes' 'lib32-libxxf86vm' 'lib32-libxcb' 'lib32-wayland' 'lib32-libgbm-git' 'khrplatform-devel-git')
  pkgdesc="Mesa EGL libraries and headers"
  conflicts=('lib32-libegl')
  provides=("lib32-libegl=${_mesaver}")

  make -C ${srcdir}/?esa-*/src/gallium/targets/egl-static DESTDIR="${pkgdir}" install
  #install -m755 -d "${pkgdir}/usr/share/doc/libegl"
  #install -m644 ${srcdir}/?esa-*/docs/egl.html "${pkgdir}/usr/share/doc/libegl/"

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/gbm DESTDIR="${pkgdir}" install

  make -C ${srcdir}/?esa-*/src/egl DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/gbm DESTDIR="${pkgdir}" uninstall
  make -C ${srcdir}/?esa-*/src/mapi/shared-glapi DESTDIR="${pkgdir}" uninstall

  rm -r "$pkgdir"/usr/include
  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-libegl"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-libegl/"

  # fix file conflicts
  rm -rf ${pkgdir}/usr/include/KHR
}

package_lib32-ati-dri-git() {
  depends=("lib32-libgl-git")
  pkgdesc="Mesa DRI radeon/r200 + Gallium3D r300,r600 drivers for AMD/ATI Radeon"
  conflicts=('lib32-ati-dri')
  provides=("lib32-ati-dri=${_mesaver}")

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  # classic mesa drivers for radeon,r200
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/radeon DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/r200 DESTDIR="${pkgdir}" install
  # gallium3D driver for r300,r600,radeonsi
  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-r300 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-r600 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-radeonsi DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-ati-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-ati-dri/"
}

package_lib32-intel-dri-git() {
  depends=("lib32-libgl-git")
  pkgdesc="Mesa DRI drivers for Intel"
  conflicts=("lib32-intel-dri")
  provides=("lib32-intel-dri=${_mesaver}")

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/i915 DESTDIR="${pkgdir}" install
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/i965 DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-intel-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-intel-dri/"
}

package_lib32-nouveau-dri-git() {
  depends=("lib32-libgl-git")
  pkgdesc="Mesa classic DRI + Gallium3D drivers for Nouveau"
  conflicts=('lib32-nouveau-dri')
  provides=("lib32-nouveau-dri=${_mesaver}")

  # fix linking because of splitted package
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" install

  # classic mesa driver for nv10 , nv20 nouveau_vieux_dri.so
  make -C ${srcdir}/?esa-*/src/mesa/drivers/dri/nouveau DESTDIR="${pkgdir}" install
  # gallium3D driver for nv30 - nv40 - nv50 nouveau_dri.so
  make -C ${srcdir}/?esa-*/src/gallium/targets/dri-nouveau DESTDIR="${pkgdir}" install

  # fix linking because of splitted package - cleanup
  make -C ${srcdir}/?esa-*/src/mesa/libdricore DESTDIR="${pkgdir}" uninstall

  install -m755 -d "${pkgdir}/usr/share/licenses/lib32-nouveau-dri"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/lib32-nouveau-dri/"
}

