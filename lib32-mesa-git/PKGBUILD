# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_mesaver=9.1
pkgname=lib32-mesa-git
pkgdesc="Open-source implementation of the OpenGL specification, device drivers ranging from software emulation to complete hardware acceleration for modern GPUs."
pkgver=20130111
pkgrel=1500
arch=('x86_64')
makedepends=('glproto-git' 'lib32-libdrm-git' 'lib32-libxxf86vm>=1.1.2' 'lib32-libxdamage>=1.1.3' 'lib32-expat>=2.1.0' 'lib32-libx11>=1.5.0' 'lib32-libxt>=1.1.3'
             'gcc-multilib' 'dri2proto-git' 'python2' 'libxml2' 'lib32-llvm-amdgpu-git' 'lib32-systemd')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=(LICENSE)
provides=('lib32-libglapi=${_mesaver}' 'lib32-libgl=${_mesaver}' 'lib32-mesa=${_mesaver}' 'lib32-osmesa=${_mesaver}'
          'lib32-libgbm=${_mesaver}' 'lib32-libgles=${_mesaver}' 'lib32-libegl=${_mesaver}' 'lib32-ati-dri=${_mesaver}'
          'lib32-intel-dri=${_mesaver}' 'lib32-nouveau-dri=${_mesaver}')
conflicts=('lib32-libglapi-git' 'lib32-libgl-git' 'lib32-mesa-git' 'lib32-osmesa-git'
          'lib32-libgbm-git' 'lib32-libgles-git' 'lib32-libegl-git' 'lib32-ati-dri-git'
          'lib32-intel-dri-git' 'lib32-nouveau-dri-git')
replaces=('lib32-libglapi-git' 'lib32-libgl-git' 'lib32-mesa-git' 'lib32-osmesa-git'
          'lib32-libgbm-git' 'lib32-libgles-git' 'lib32-libegl-git' 'lib32-ati-dri-git'
          'lib32-intel-dri-git' 'lib32-nouveau-dri-git')
md5sums=('5c65a0fe315dd347e09b1f2826a1df5a')

_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa-build'

build() {
  cd "${srcdir}"
  msg 'Connecting to GIT server....'
  if [ -d ${_gitname} ] ; then
    cd ${_gitname} && git clean -xfd && git checkout -f && git pull origin
  else
    git clone --depth 1 ${_gitroot} ${_gitname}
  fi
  msg 'GIT checkout done or server timeout'
  msg 'Starting make...'

  cd "${srcdir}/${_gitname}"

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"
  # for our llvm-config for 32 bit
  export LLVM_CONFIG=/usr/bin/llvm-config32

  # --with-dri-driverdir=/usr/lib32/xorg/modules/dri \
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-32-bit \
    --libdir=/usr/lib32 \
    --enable-gallium-llvm \
    --enable-egl \
    --enable-gallium-egl \
    --with-egl-platforms=drm,x11 \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-r600-llvm-compiler

  make
}

package() {
  cd "${srcdir}/${_gitname}"

  make DESTDIR="${pkgdir}" install
  rm -rf ${pkgdir}/etc ${pkgdir}/usr/include
}

