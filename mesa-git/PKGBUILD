# $Id$
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Maintainer: Andreas Radke <andyrtr@archlinux.org>

_realver=9.2.0-devel
_gitroot='git://anongit.freedesktop.org/git/mesa/mesa'
_gitname='mesa'

pkgbase=mesa-git
pkgver=20130710
pkgrel=0734
pkgname=('ati-dri-git' 'intel-dri-git' 'nouveau-dri-git' 'svga-dri-git' 'mesa-git' 'mesa-libgl-git')

arch=('i686' 'x86_64')
depends=('gcc-libs' 'glibc' 'libdrm' 'expat' 'libxt' 'libxxf86vm'
          'libxdamage' 'libx11' 'libxcb' 'libxfixes' 'libxext'
          'libxvmc' 'wayland')
makedepends=('glproto' 'expat' 'gcc-libs' 'dri2proto' 'libtool'
              'python2' 'libxml2' 'llvm-git' 'systemd'
              'libvdpau')
optdepends=('opengl-man-pages: for the OpenGL API man pages')
url="http://mesa3d.sourceforge.net"
license=('custom')
options=('!libtool')
source=($_gitroot LICENSE)
md5sums=('SKIP' '5c65a0fe315dd347e09b1f2826a1df5a')

build() {
  cd "${srcdir}/${_gitname}"

    #--enable-gallium-egl \
  ./autogen.sh \
    --prefix=/usr \
    --sysconfdir=/etc \
    --disable-static \
    --with-gallium-drivers=r300,r600,radeonsi,nouveau,svga,swrast \
    --with-dri-drivers=i915,i965,r200,radeon,nouveau,swrast \
    --enable-gallium-llvm \
    --enable-egl \
    --with-egl-platforms=drm,x11,wayland \
    --enable-shared-glapi \
    --enable-gbm \
    --enable-glx-tls \
    --enable-dri \
    --enable-glx \
    --enable-osmesa \
    --enable-gles1 \
    --enable-gles2 \
    --enable-texture-float \
    --enable-xa \
    --enable-xvmc \
    --enable-vdpau \
    --with-llvm-shared-libs \
    --enable-r600-llvm-compiler

  make

  msg 'Starting fake install..'
  rm -rf $srcdir/fakeinstall
  mkdir $srcdir/fakeinstall
  make -j1 DESTDIR=${srcdir}/fakeinstall install
}

package_ati-dri-git() {
  depends=("mesa-libgl-git")
  pkgdesc="Mesa drivers for AMD/ATI Radeon"
  provides=('ati-dri=${_realver}')
  conflicts=('ati-dri' 'xf86-video-ati<6.9.0-6')

  install -m755 -d ${pkgdir}/usr/lib/
  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  install -m755 -d ${pkgdir}/usr/lib/dri/
  #install -m755 -d ${pkgdir}/usr/lib/gallium-pipe/

  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_{r300,r600,radeonsi}.* ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{r200,r300,r600,radeon,radeonsi}_dri.so ${pkgdir}/usr/lib/dri/
  #mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_{r300,r600,radeonsi}* ${pkgdir}/usr/lib/gallium-pipe/
  mv -v ${srcdir}/fakeinstall/usr/lib/libXvMCr600.* ${pkgdir}/usr/lib/
  mv -v ${srcdir}/fakeinstall/usr/lib/libXvMCr300.* ${pkgdir}/usr/lib/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_intel-dri-git() {
  pkgdesc="Mesa drivers for Intel"
  depends=("mesa-libgl-git")
  provides=('intel-dri=${_realver}')
  conflicts=('intel-dri')
  replaces=('intel-dri')

  install -m755 -d ${pkgdir}/usr/lib/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/{i915,i965}_dri.so ${pkgdir}/usr/lib/dri/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_nouveau-dri-git() {
  pkgdesc="Mesa drivers for Nouveau"
  depends=("mesa-libgl-git")
  provides=('nouveau-dri=${_realver}')
  conflicts=('nouveau-dri')
  replaces=('nouveau-dri')

  install -m755 -d ${pkgdir}/usr/lib/vdpau/
  install -m755 -d ${pkgdir}/usr/lib/dri
  #install -m755 -d ${pkgdir}/usr/lib/gallium-pipe

  mv -v ${srcdir}/fakeinstall/usr/lib/vdpau/libvdpau_nouveau.* ${pkgdir}/usr/lib/vdpau/
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/nouveau_{dri,vieux_dri}.so ${pkgdir}/usr/lib/dri/

  #mv -v ${srcdir}/fakeinstall/usr/lib/libXvMCnouveau* ${pkgdir}/usr/lib/gallium-pipe/
  #mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_nouveau* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_svga-dri-git() {
  pkgdesc="Gallium3D VMware guest GL driver"
  depends=("mesa-libgl-git" "gcc-libs" "libdrm" "expat" "libffi")

  install -m755 -d ${pkgdir}/usr/lib/dri
  mv -v ${srcdir}/fakeinstall/usr/lib/dri/vmwgfx_dri.so ${pkgdir}/usr/lib/dri/

  #install -m755 -d ${pkgdir}/usr/lib/gallium-pipe
  #mv -v ${srcdir}/fakeinstall/usr/lib/gallium-pipe/pipe_vmwgfx* ${pkgdir}/usr/lib/gallium-pipe/

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-git() {
  pkgdesc="an open-source implementation of the OpenGL specification"
  depends=('libdrm' 'libvdpau' 'wayland' 'libxxf86vm' 'libxdamage' 'systemd')
  optdepends=('opengl-man-pages: for the OpenGL API man pages')
  provides=('mesa=${_realver}' 'libglapi=${_realver}' 'osmesa=${_realver}' 'libgbm=${_realver}' 'libgles=${_realver}' 'libegl=${_realver}' 'khrplatform-devel=${_realver}')
  conflicts=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')
  replaces=('libglapi' 'osmesa' 'libgbm' 'libgles' 'libegl' 'khrplatform-devel')

  mv -v ${srcdir}/fakeinstall/* ${pkgdir}
  # rename libgl.so to not conflict with blobs - may break gl.pc ?
  mv ${pkgdir}/usr/lib/libGL.so.1.2.0   ${pkgdir}/usr/lib/mesa-libGL.so.1.2.0
  rm ${pkgdir}/usr/lib/libGL.so{,.1}

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

package_mesa-libgl-git(){
  pkgdesc="Mesa 3-D graphics library"
  depends=("mesa-git")
  provides=("libgl=${_realver}")
  replaces=('libgl')

  # See FS#26284
  install -m755 -d "${pkgdir}/usr/lib/xorg/modules/extensions"
  ln -s libglx.xorg "${pkgdir}/usr/lib/xorg/modules/extensions/libglx.so"

  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1
  ln -s mesa-libGL.so.1.2.0      ${pkgdir}/usr/lib/libGL.so.1.2.0

  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 "${srcdir}/LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/"
}

# vim: ft=sh syn=sh et
